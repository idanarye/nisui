/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'idanarye' at '9/10/16 11:09 PM' with Gradle 2.14
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.14/userguide/tutorial_java_projects.html
 */

apply plugin: 'java'
apply plugin: 'application'
mainClassName = 'nisui.app.NisuiApp'

allprojects {
    repositories {
        jcenter()
    }

    apply plugin: 'java'
    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.21'
        testCompile 'junit:junit:4.12'
    }
    test {
        outputs.upToDateWhen { false }
        testLogging.showStandardStreams = true
    }

    compileJava {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

run {
   standardInput = System.in
   enableAssertions = true
   if (project.hasProperty('mainClass')) {
      main = mainClass
   }
   if (project.hasProperty('args')) {
      args(project.getProperty('args').split(' '))
   }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Nisui',
                   /*'Implementation-Version': version,*/
                   'Main-Class': 'nisui.app.NisuiApp'
    }
    baseName = 'Nisui'
    destinationDir = file('build')
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

subprojects {
    dependencies {
        testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.6.2'
        testCompile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.6.2'
    }
}

project(":java_runner") {
    dependencies {
        compile project(':core')
    }
}

project(":h2_store") {
    dependencies {
       compile group: 'com.h2database', name: 'h2', version: '1.4.196'
       compile project(':core')

       // Use this to create test data
       testCompile project(':java_runner')
       testCompile 'org.projectlombok:lombok:1.16.12'
    }
}


// App project's settings

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.6.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.6.2'

    /*// Declare the dependency for your favourite test framework you want to use in your tests.*/
    /*// TestNG is also supported by the Gradle Test task. Just change the*/
    /*// testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add*/
    /*// 'test.useTestNG()' to your build script.*/

    compile project(':core')
    compile project(':java_runner')
}

test {
    testLogging.showStandardStreams = true
}
